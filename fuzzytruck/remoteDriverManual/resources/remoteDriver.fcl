FUNCTION_BLOCK remoteDriver

VAR_INPUT
	x : REAL;
	y : REAL;
	direction : REAL;
END_VAR

VAR_OUTPUT
	angle : REAL;
END_VAR

FUZZIFY x
	TERM tooLeft := (0, 1) (0.3, 0);
	TERM left := (0.1, 0) (0.3, 1) (0.5, 0);
	TERM center := (0.4, 0) (0.5, 1) (0.6, 0);
	TERM right := (0.5, 0) (0.7, 1) (0.9, 0);
	TERM tooRight := (0.7, 0) (1, 1);
END_FUZZIFY

FUZZIFY direction
    TERM left  := (90, 0) (180, 1) (270, 0);
    TERM right := (0, 1) (90, 0) (270, 0) (360, 1);
    TERM up    := (0, 0) (90, 1) (180, 0);
    TERM down  := (180, 0) (270, 1) (360, 0);
END_FUZZIFY

DEFUZZIFY angle
    TERM tooLeft  :=  (-1, 1) (-0.4, 0);
    TERM left     := (-0.8, 0) (-0.6, 1) (0, 0);
    TERM center   := (-0.3, 0) (0, 1) (0.3, 0);
    TERM right    := (0, 0) (0.6, 1) (0.8, 0);
    TERM tooRight := (0.4, 0) (1, 1);
   	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;
	ACT : MIN;

    RULE 1: IF x IS tooLeft AND direction IS left THEN angle IS tooLeft;
    RULE 2: IF x IS tooLeft AND direction IS right THEN angle IS tooRight;
    RULE 3: IF x IS tooLeft AND direction IS up THEN angle IS right;
    RULE 4: IF x IS tooLeft AND direction IS down THEN angle IS tooRight;
    RULE 5: IF x IS left AND direction IS left THEN angle IS tooLeft;
    RULE 6: IF x IS left AND direction IS right THEN angle IS right;
    RULE 7: IF x IS left AND direction IS up THEN angle IS right;
    RULE 8: IF x IS left AND direction IS down THEN angle IS tooLeft;
    RULE 9: IF x IS center AND direction IS left THEN angle IS tooLeft;
    RULE 10: IF x IS center AND direction IS right THEN angle IS tooRight;
    RULE 11: IF x IS center AND direction IS up THEN angle IS center;
    RULE 12: IF x IS center AND direction IS down THEN angle IS tooLeft;
    RULE 13: IF x IS right AND direction IS left THEN angle IS left;
    RULE 14: IF x IS right AND direction IS right THEN angle IS tooRight;
    RULE 15: IF x IS right AND direction IS up THEN angle IS left;
    RULE 16: IF x IS right AND direction IS down THEN angle IS tooRight;
    RULE 17: IF x IS tooRight AND direction IS left THEN angle IS left;
    RULE 18: IF x IS tooRight AND direction IS right THEN angle IS tooRight;
    RULE 19: IF x IS tooRight AND direction IS up THEN angle IS left;
    RULE 20: IF x IS tooRight AND direction IS down THEN angle IS tooRight;

END_RULEBLOCK

END_FUNCTION_BLOCK